#!/usr/bin/gnuplot
#
# Demonstration of a common use scenario of the multiplot environment.
#
# AUTHOR: Hagen Wierstorf
#

reset

# wxt terminal
set terminal pngcairo enhanced

set output "Multiplot.png"

# color definitions
set style line  1 lc rgb '#0060ad' pt 5 ps 0.2 lt 1 lw 2    # blue

unset key

# Enable the use of macros
set macros

# Functions (1/0 means not defined)
a = 0.9

w=0.3
wa=0.5
wb=0.8
wc=1
Xo=0

f(x,w)  = a*sin(w*x)**2
v(x,w)  = a*(x/2 - sin(2*w*x)/(4*w))
x(x,w)  = a*((x**2)/(4) + (cos(2*a*x)-1)/(8*a**2 ) ) + Xo
x1(x,w) = a*((x**2)/(4) + (cos(2*a*x)-1)/(8*a**2 ) ) + Xo

set tics scale 0.5
set ytics 1
set xrange [-2.5*pi:2.5*pi]
set yrange [-1.5:1.5]

# MACROS
# x- and ytics for each row resp. column
NOXTICS = "set xtics  autofreq; \
          unset xlabel"
XTICS = "set xtics  autofreq;\
          set xlabel 'x'"
NOYTICS = "set format y ''; unset ylabel"
YTICS = "set format y '%.0f'; set ylabel 'y'"
# Margins for each row resp. column
TMARGIN = "set tmargin at screen 0.90; set bmargin at screen 0.55"
BMARGIN = "set tmargin at screen 0.55; set bmargin at screen 0.20"
LMARGIN = "set lmargin at screen 0.15; set rmargin at screen 0.55"
RMARGIN = "set lmargin at screen 0.55; set rmargin at screen 0.95"
# Placement of the a,b,c,d labels in the graphs
POS = "at graph 0.92,0.9 font ',8'"

### Start multiplot (2x2 layout)
set multiplot layout 2,2 rowsfirst
# --- GRAPH a
@TMARGIN; @LMARGIN
@NOXTICS; @YTICS
set label 1 'a' @POS
plot f(x,w) with lines ls 1
# --- GRAPH b
@TMARGIN; @RMARGIN
@NOXTICS; @NOYTICS
set label 1 'b' @POS
plot v(x,w) with lines ls 1
# --- GRAPH c
@BMARGIN; @LMARGIN
@XTICS; @YTICS
set label 1 'c' @POS
plot x(x,w) with lines ls 1
# --- GRAPH d
@BMARGIN; @RMARGIN
@XTICS; @NOYTICS
set label 1 'd' @POS
plot x1(x,w) with lines ls 1
unset multiplot
### End multiplot
